apply plugin: 'maven-publish'

publishing {
    publications {
        maven(MavenPublication) {

            artifact("$buildDir/libs/${project.name}-${version}.jar")

            pom.withXml {

                asNode().appendNode('name', "${project.name}")
                asNode().appendNode('description', "\n${new File("${project.rootDir}/releaseNotes.md").getText('UTF-8')}\n")
                def dependenciesNode = asNode().appendNode('dependencies')

                def adder = {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
                configurations.implementation.allDependencies.each(adder)
            }
        }
    }
    repositories {
        maven {
            url project.hasProperty("maven_repo") ? maven_repo : ""
            credentials {
                username project.hasProperty("archiva_login") ? archiva_login : ""
                password project.hasProperty("archiva_password") ? archiva_password : ""
            }
        }
    }
}

